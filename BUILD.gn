executable("mimir") {
    libs = []
    deps = [ ":tree_sitter" ]

    defines = [
        "APP_INPUT_ID_START=0x100",
        "GUI_INPUT_ID_START=0x200",
    ]

    assets_dir = rebase_path("//assets", "")
    defines += [ "ASSETS_DIR=\"" + assets_dir + "\"" ]

    include_dirs = [
        root_build_dir,
        "external",
    ]

    sources = [
        "src/assets.cpp",
        "src/font.cpp",
        "src/gfx_opengl.cpp",
        "src/core.cpp",
        "src/memory.cpp",
        "src/string.cpp",
    ]

    if (current_os == "win") {
        sources += [
            "src/win32_mimir.cpp",
            "src/win32_opengl.cpp",
            "src/win32_window.cpp",

            "src/win32_file.cpp",
            "src/win32_memory.cpp",
            "src/win32_thread.cpp",
        ]

        libs += [
            "user32",
            "shell32",
            "opengl32",
            "gdi32",
            "shlwapi",
        ]
    } else if (current_os == "linux") {
        sources += [
            "src/linux_opengl.cpp",
            "src/linux_window.cpp",

            "src/linux_file.cpp",
            "src/linux_memory.cpp",
            "src/linux_thread.cpp",
        ]
    }
}

group("tools") {
    deps = [ "//tools/gh" ]
}

action_foreach("gh") {
    deps = [ "//tools/gh" ]
    script = "tools/exe.py"
    sources = [
        "src/gui.cpp",

        "src/gfx_opengl.cpp",
        "src/window.cpp",
        "src/font.cpp",
        "src/assets.cpp",

        "src/maths.cpp",
        "src/string.cpp",
    ]


    mnemonic = "generate-headers"

    outputs = [
        "$target_gen_dir/{{source_name_part}}.h",
        "$target_gen_dir/internal/{{source_name_part}}.h",
    ]

    if (current_os == "win") { exe = rebase_path(root_build_dir, "") + "/gh.exe" }
    else { exe = rebase_path(root_build_dir, "") + "/gh" }

    args = [
        exe,
        "{{source}}",
        "-o", rebase_path(target_gen_dir, ""),
        "--",
        "-I", rebase_path("//external/flecs/include", "")
    ]
}

config("tree_sitter_public") {
  include_dirs = [ "external/tree-sitter-0.20.6/lib/include" ]
}

static_library("tree_sitter") {
    TS_DIR = "external/tree-sitter-0.20.6"
    TS_CPP_DIR = "external/tree-sitter-cpp-master"
    TS_RS_DIR = "external/tree-sitter-rust-0.20.1"
    TS_BASH_DIR = "external/tree-sitter-bash-master"
    TS_CS_DIR = "external/tree-sitter-c-sharp-0.19.1"
    TS_LUA_DIR = "external/tree-sitter-lua-master"
    TS_COMMENT_DIR = "external/tree-sitter-comment-master"

    sources = [
        "$TS_DIR/lib/src/lib.c",
        "$TS_CPP_DIR/src/parser.c",
        "$TS_CPP_DIR/src/scanner.cc",
        "$TS_RS_DIR/src/parser.c",
        "$TS_RS_DIR/src/scanner.c",
        "$TS_BASH_DIR/src/parser.c",
        "$TS_BASH_DIR/src/scanner.cc",
        "$TS_CS_DIR/src/parser.c",
        "$TS_CS_DIR/src/scanner.c",
        "$TS_LUA_DIR/src/parser.c",
        "$TS_LUA_DIR/src/scanner.c",
        "$TS_COMMENT_DIR/src/parser.c",
        "$TS_COMMENT_DIR/src/scanner.c",
    ]

    configs += ["//gn/config:optimize", "//gn/config:no_warnings"]
    include_dirs = ["$TS_DIR/lib/include"]
    public_configs = [":tree_sitter_public"]














}
